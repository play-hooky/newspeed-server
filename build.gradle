plugins {
	id 'java'
	id 'jacoco'
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.1.2'
	id 'org.jetbrains.kotlin.jvm' version '1.6.21'
	id 'org.jetbrains.kotlin.kapt' version '1.6.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21'
	id 'org.jetbrains.kotlin.plugin.jpa' version '1.6.21'
}

group = 'com.newspeed'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('testcontainersVersion', "1.17.2")
	set('springCloudVersion', '2021.0.3')
}

dependencies {
	implementation("org.springframework.boot:spring-boot-starter")
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation("org.jetbrains.kotlin:kotlin-reflect")
	testImplementation("org.springframework.boot:spring-boot-starter-test")
	testImplementation("io.mockk:mockk:1.13.4")

	// jackson
	implementation("com.fasterxml.jackson.module:jackson-module-kotlin")

	// jpa
	implementation("org.springframework.boot:spring-boot-starter-data-jpa")

	// rdbms
	runtimeOnly("com.h2database:h2")
	runtimeOnly('mysql:mysql-connector-java')
	testImplementation('org.testcontainers:mysql:1.17.2')
	testImplementation('org.testcontainers:testcontainers:1.16.0')
	testImplementation('org.testcontainers:junit-jupiter:1.16.2')
	testImplementation('org.testcontainers:jdbc:1.16.0')

	// querydsl
	implementation('com.querydsl:querydsl-jpa:5.0.0')
	kapt('com.querydsl:querydsl-apt:5.0.0:jpa')

	// JWT
	implementation('io.jsonwebtoken:jjwt-api:0.11.5')
	runtimeOnly('io.jsonwebtoken:jjwt-impl:0.11.5')
	runtimeOnly('io.jsonwebtoken:jjwt-jackson:0.11.5')

	// bean validation
	implementation('org.hibernate.validator:hibernate-validator:6.1.2.Final')
	implementation('org.glassfish:jakarta.el:3.0.3')

	// openfeign
	implementation("org.springframework.cloud:spring-cloud-starter-openfeign")

	// redis
	implementation('org.springframework.boot:spring-boot-starter-data-redis')
	implementation("it.ozimov:embedded-redis:0.7.2") { exclude( group: "org.slf4j", module: "slf4j-simple") }

	// Flyway
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'

	// thymeleaf
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

	// gson
	implementation 'com.google.code.gson:gson'

	// kotest
	testImplementation("io.kotest:kotest-runner-junit5:5.5.5")
	testImplementation("io.kotest:kotest-assertions-core:5.5.5")
	testImplementation("io.kotest.extensions:kotest-extensions-spring:1.1.2")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

jacoco {
	toolVersion = '0.8.7'
}

jacocoTestReport {
	reports {
		html.required.set(true)
		xml.required.set(true)
		csv.required.set(false)
	}

	def QClass = []
	for (qPattern in "**/QA".."**/QZ") {
		QClass.add(qPattern + "*")
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: [
							"**/NewspeedServerApplication*",
							'**/global*'
					] + QClass)
		}))
	}
	finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
	def QClass = []
	for (qPattern in '*.QA'..'*.QZ') {
		QClass.add(qPattern + '*')
	}

	violationRules {
		rule {
			enabled = true
			element = 'BUNDLE'

			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
//                minimum = 0.80
			}

			excludes = [
					'**.NewspeedServerApplication*',
					'**/global*'
			] + QClass
		}
	}
}

jacocoTestReport.doFirst {
	delete file('src/main/resources/static/reports')
}
